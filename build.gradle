/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.github.ben-manes.versions' version '0.13.0'
}

apply plugin: 'java'

group = 'cd.go.contrib'
version = '0.7.0'
description = 'GoCD Docker Elastic Agents'

// these values that go into plugin.xml
project.ext.pluginDesc = [
    id         : 'cd.go.contrib.elastic-agent.docker',
    version    : project.version,
    goCdVersion: '16.12.0',
    name       : 'Docker Elastic Agent Plugin',
    description: 'Docker Based Elastic Agent Plugins for GoCD',
    vendorName : 'GoCD Contributors',
    vendorUrl  : 'https://github.com/gocd-contrib/docker-elastic-agents'
]

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildDir = "${projectDir}/target"

repositories {
  jcenter()
  mavenLocal()
}

sourceSets {
  test {
    java {
      compileClasspath += configurations.compileOnly
      runtimeClasspath += configurations.compileOnly
    }
  }
}

dependencies {
  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '16.11.0'

  compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
  compile group: 'com.spotify', name: 'docker-client', version: '8.2.0'
  compile group: 'com.google.guava', name: 'guava', version: '19.0'
  compile group: 'joda-time', name: 'joda-time', version: '2.9.4'
  compile group: 'commons-io', name: 'commons-io', version: '2.5'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.96-beta'
  testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.3.0'
}

processResources {
  from("src/main/resource-templates") {
    filesMatching('*') {
      expand project.pluginDesc
    }
  }
}

test {
  maxParallelForks = 1
  testLogging {
    showStandardStreams = true

    exceptionFormat 'full'

    beforeSuite { suite ->
      if (suite.parent) {
        logger.quiet("Running ${suite.name}")
      }
    }

    afterSuite { suite, result ->
      if (suite.parent) {
        logger.quiet("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
        if (result.resultType == TestResult.ResultType.FAILURE) {
          logger.quiet("Test ${suite.name} FAILED")
        }
      } else {
        logger.quiet("Total tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
      }
    }
  }
}

jar {
  from(configurations.compile) {
    into "lib/"
  }
  from(sourceSets.main.java) {
    into "/"
  }
}
